buildscript {
    ext {
        profile = project.hasProperty('profile') ? project.getProperty('profile') : 'dev'
        springVersion = '4.3.30.RELEASE'
        springSecurityVersion = '4.2.3.RELEASE'
    }

    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-jvm:3.3.2'
    }
}

subprojects {
    group = 'com.bezzangss'
    version = '0.0.0'

    apply plugin: 'java'
    apply plugin: 'org.asciidoctor.jvm.convert'

    java {
        sourceCompatibility = '1.8'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }

        asciidoctorClasspath
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation platform("org.springframework:spring-framework-bom:${springVersion}")
        implementation 'org.springframework:spring-core'
        implementation 'org.springframework:spring-web'
        implementation 'org.springframework:spring-webmvc'
        implementation 'org.springframework:spring-orm'
        implementation 'org.springframework:spring-context-support'
        implementation 'org.springframework:spring-aop'
        implementation 'org.springframework:spring-test'

        implementation platform("org.springframework.security:spring-security-bom:${springSecurityVersion}")
        implementation 'org.springframework.security:spring-security-web'
        implementation 'org.springframework.security:spring-security-config'

        // aop
        implementation 'org.aspectj:aspectjweaver:1.9.5'

        // junit
        implementation 'junit:junit:4.13.2'
        implementation 'org.junit.vintage:junit-vintage-engine:5.10.0'

        // restdocs
        implementation 'org.springframework.restdocs:spring-restdocs-mockmvc:1.2.6.RELEASE'
        asciidoctorClasspath 'org.asciidoctor:asciidoctorj:1.5.8'

        // servlet
        compileOnly 'javax.servlet:javax.servlet-api:3.0.1'
        testImplementation 'javax.servlet:javax.servlet-api:3.0.1'

        // lombok
        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        testCompileOnly 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

        // mapstruct
        implementation 'org.mapstruct:mapstruct:1.5.5.Final'
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

        // logback
        implementation 'ch.qos.logback:logback-classic:1.2.12'

        // jackson
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.17.2'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2'

        // mustache
        implementation 'com.github.spullara.mustache.java:compiler:0.9.10'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    // restdoc
    task asciidocInternal(type: org.asciidoctor.gradle.jvm.AsciidoctorTask) {
        dependsOn ':sign:asciidocCss'
        dependsOn ':sign-common:asciidocErrorCode'

        sourceDir = file("src/adoc")
        outputDir = file("build/adoc/internal")

        sources {
            include "internal.adoc"
        }

        inputs.dir("build/generated-snippets-web-internal")

        attributes 'snippets': file("build/generated-snippets-web-internal"),
                'revnumber': version,
                'stylesheet': 'adoc.css',
                'stylesdir': 'resources/css',
                'linkcss': false

        doFirst {
            baseDir = sourceDir
            def cssDir = file("$buildDir/adoc/css")
            def css = new File(cssDir, "adoc.css")
            file("$sourceDir/resources/css/adoc.css").text = css.text
        }
    }
    build.dependsOn asciidocInternal
}

project(':sign') {
    apply plugin: 'war'

    dependencies {
        implementation project(':sign-config')
        implementation project(':sign-common')
        implementation project(':sign-domain')
        implementation project(':sign-application')
        implementation project(':sign-adapter-repository-jpa')
        implementation project(':sign-adapter-storage-filesystem')
        implementation project(':sign-adapter-storage-ftp')
        implementation project(':sign-adapter-web')
        implementation project(':sign-adapter-web-internal')
    }

    tasks.withType(War) {
        project.sourceSets.main.resources {
            srcDir 'src/main/resources'
            exclude 'properties/application-*.properties'
        }

        project.tasks.named('processResources') {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE

            filesMatching('properties/application.properties') {
                expand(profile: profile)
            }

            from('src/main/resources') {
                include "properties/application-${profile}.properties"
            }
        }
    }

    war.enabled(true)
}

project(':sign-config') {
    dependencies {
    }

    jar.enabled(true)
}

project(':sign-common') {
    dependencies {
    }

    jar.enabled(true)
}

project(':sign-domain') {
    dependencies {
        implementation project(':sign-common')
    }

    jar.enabled(true)
}

project(':sign-application') {
    dependencies {
        implementation project(':sign-common')
        implementation project(':sign-domain')
    }

    jar.enabled(true)
}

project(':sign-adapter-repository-jpa') {
    dependencies {
        implementation project(':sign-common')
        implementation project(':sign-application')
    }

    jar.enabled(true)
}

project(':sign-adapter-storage-filesystem') {
    dependencies {
        implementation project(':sign-common')
        implementation project(':sign-application')
    }

    jar.enabled(true)
}

project(':sign-adapter-storage-ftp') {
    dependencies {
        implementation project(':sign-common')
        implementation project(':sign-application')
    }

    jar.enabled(true)
}

project(':sign-adapter-web') {
    dependencies {
        implementation project(':sign-common')
        implementation project(':sign-application')
    }

    jar.enabled(true)
}

project(':sign-adapter-web-internal') {
    dependencies {
        implementation project(':sign-common')
        implementation project(':sign-application')
        implementation project(':sign-adapter-web')
    }

    jar.enabled(true)
}
